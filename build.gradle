plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0-RC2'
    id 'io.spring.dependency-management' version '1.1.0'
    id "io.freefair.lombok" version "8.0.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.wolflink.minecraft.wolfird'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT"
    testImplementation "com.github.seeseemelk:MockBukkit-v1.18:2.7.0"
    implementation 'com.google.inject:guice:7.0.0-rc1'
    implementation 'org.hibernate:hibernate-core:6.2.1.Final'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'mysql:mysql-connector-java:8.0.32'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
sourceSets{
    main {
        java {
            exclude 'test/**'
        }
    }
}
bootJar{
    enabled = false
}